version = '2.2.0'
group = 'kaizen'

apply from: '../common.gradle'

buildscript {
	dependencies {
		classpath 'kaizen:kaizen:0.2.4'
	}
	repositories {
		ivy { url 'http://unity-technologies.github.com/kaizen/repositories/unstable' }
	}
}

repositories {
	ivy { url 'http://unity-technologies.github.com/kaizen/repositories/unstable' }
}

def ikvmVersion = '7.1.4532.2'
dependencies {
	ext {
		ikvm = { name -> "IKVM.NET:$name:$ikvmVersion" }
	}
}

apply plugin: 'base'
apply plugin: 'unity'
apply plugin: 'kaizen-lib-client'

configurations {
	ivy
	IKVM
}

dependencies {
	ivy "org.apache.ivy:ivy:$version"
	IKVM "${ikvm('ikvmc')}"
	['IKVM.Runtime', 'IKVM.OpenJDK.XML.Parse'].each {
		'default' "${ikvm(it)}"
	}
}

repositories {
	mavenCentral()
}

task downloadJars(type: Copy) {
	from configurations.ivy
	into project.buildDir
}

def outputFileName = "${project.name}.dll"
def outputFile = "${project.buildDir}/${outputFileName}"

task convert(type: Exec, dependsOn: [updateIKVM, downloadJars]) {
	inputs.source configurations.ivy
	outputs.file outputFile
	executable rootProject.unity.mono.cli
	args "lib/IKVM/ikvmc.exe"
	args "-out:$outputFile"
	args "-assembly:${project.name}"
	args "-version:$version"
	args inputs.files
}

task zip(type: Zip, dependsOn: convert) {
	from project.buildDir
	include outputFileName
}

artifacts {
	'default' zip
}

uploadDefault {
	repositories {
		ivy { url repositoryForPublishing }
	}
}
